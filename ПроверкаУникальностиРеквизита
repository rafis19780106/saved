// Процедура проверяет уникальность значения реквизита объекта.
//	ВАЖНО!!! Реквизит должен входить в доступные поля блокировки данных
//
// Параметры:
//   Объект_п     - Объект, для которого проверяем уникальность реквизита.
//   Реквизит_п   - Имя реквизита, для которого проверяем уникальность.
//
// Пример вызова:
//   ПроверкаУникальностиРеквизита(Объект, "Наименование");
//
Процедура ПроверкаУникальностиРеквизита(Объект_п, Реквизит_п) Экспорт
	
	//	Проверка выполняется только при заданном имени реквизита
	Если Не ЗначениеЗаполнено(Реквизит_п) Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНаОбъект_л = Объект_п.Ссылка;
	
	// Получаем метаданные объекта
	ОбъектМетаданные_л = Объект_п.Метаданные();
	МетаданныеПолноеИмя_л = ОбъектМетаданные_л.ПолноеИмя();
	МетаданныеСиноним_л = ОбъектМетаданные_л.Синоним;
	
	ЗначениеРеквизита_л = Объект_п[Реквизит_п];
	
	// Блокируем данные для предотвращения дублей при параллельном выполнении
	Блокировка_л = Новый БлокировкаДанных;
	ЭлементБлокировки_л = Блокировка_л.Добавить(МетаданныеПолноеИмя_л);
	ЭлементБлокировки_л.УстановитьЗначение(Реквизит_п, ЗначениеРеквизита_л);
	Блокировка_л.Заблокировать();
	
	// Формируем запрос для поиска дублей
	Запрос_л = Новый Запрос;
	Запрос_л.Текст = "
	|ВЫБРАТЬ
	|   МетаданныеПолноеИмя.Ссылка КАК Ссылка,
	|   ПРЕДСТАВЛЕНИЕ(МетаданныеПолноеИмя.Ссылка) КАК СсылкаПредставление
	|ИЗ
	|   &МетаданныеПолноеИмя КАК МетаданныеПолноеИмя
	|ГДЕ
	|   МетаданныеПолноеИмя.Ссылка <> &Ссылка
	|   И &ПутьКРеквизиту = &ЗначениеРеквизита";
	
	// Замещаем параметры в тексте запроса
	Запрос_л.Текст = СтрЗаменить(Запрос_л.Текст, "&МетаданныеПолноеИмя", МетаданныеПолноеИмя_л);
	Запрос_л.Текст = СтрЗаменить(Запрос_л.Текст, "&ПутьКРеквизиту", СтрШаблон("МетаданныеПолноеИмя.%1", Реквизит_п));
	
	// Устанавливаем параметры запроса
	Запрос_л.УстановитьПараметр("Ссылка", СсылкаНаОбъект_л);
	Запрос_л.УстановитьПараметр("ЗначениеРеквизита", ЗначениеРеквизита_л);
	
	// Выполняем запрос и выбираем данные
	Выборка_л = Запрос_л.Выполнить().Выбрать();
	
	// Проверяем наличие дубля
	Если Выборка_л.Следующий() Тогда
		// Формируем шаблон исключения
		ШаблонИсключения_л = НСтр("ru = 'В системе уже есть элемент ""%1"" справочника ""%2"" со значением реквизита ""%3"" равным ""%4"".
		|Необходимо либо очистить значение, либо задать новое значение.'");
		
		// Вызываем исключение с указанием информации о дубле
		ВызватьИсключение СтрШаблон(ШаблонИсключения_л, Выборка_л.СсылкаПредставление, МетаданныеСиноним_л, Реквизит_п, ЗначениеРеквизита_л);
	КонецЕсли;
	
КонецПроцедуры // ПроверкаУникальностиРеквизита()
